******************************************************************************
* FUNCAO GRAFTXT
* PARAMETROS PASSADOS :
* TITULO   --> TITULO
* LARG_COL --> LARGURA DE CADA BARRA NO GRAFICO
* LR       --> MENOR VALOR Y-AXIS
* HR       --> MAIOR VALOR Y-AXIS
* COLS[n]  --> VETOR COM OS VALORES DAS COLUNAS
* TITS[n]  --> VETOR COM OS TITULOS DAS COLUNAS
* CORES[n] --> VETOR COM AS CORES DAS COLUNAS
* CARAC[n] --> VETOR COM OS CARACTERES DAS COLUNAS (PODE SER APENAS UM CHR)
******************************************************************************
#include "SET.CH"
Function GrafTXT( TITULO,LARG_COL,LR,HR,COLS,TITS,CORES,CARAC )

LOCAL NO_COL,TWIDTH,BLINE,CTD,XCHAR,YCHAR,BCHAR,LINE,XLINE,BAR,CNTR,BCOUNT,;
      COLPOS,DIV,LEFT,CORB,CORF,LP,YN,CAUX,OLDCOR,BLANKS,GLINE,VNAME,LCOUNT,;
      CCO,CCOUNT,FT
IF .NOT. SET(_SET_PRINTER)
  OLDCOR=SETCOLOR()
  IF CORES==NIL
     CORES:=ARRAY(LEN(COLS))
     AFILL(CORES,SETCOLOR())
  ENDIF
  IF TYPE("CARAC")="C"
     CAUX=CARAC
     CARAC:=ARRAY(LEN(COLS))
     AFILL(CARAC,CAUX)
  ELSEIF TYPE("CARAC")#"A"
     CARAC:=ARRAY(LEN(COLS))
     AFILL(CARAC,CHR(219))
  ENDIF

  @ 0,0 CLEAR TO MAXROW()-2,79

  NO_COL  = LEN(COLS)
  TWIDTH  = LARG_COL-1
  BLINE   = SPACE(6)

  FOR CTD = 1 TO NO_COL

   TITS[CTD] = ALLTRIM(TITS[CTD])
   TITS[CTD] = LEFT(SPACE(CENTRA(TITS[CTD],TWIDTH))+TITS[CTD],TWIDTH)
   TITS[CTD] = TITS[CTD]+IF(LEN(TITS[CTD])<TWIDTH,SPACE(TWIDTH-LEN(TITS[CTD])),"")
   BLINE     = BLINE+TITS[CTD]+SPACE(1)

  NEXT

  TITULO = SPACE((((LARG_COL*NO_COL)+5)/2)-(LEN(TRIM(TITULO))/2))+TITULO

  XCHAR = CHR(196)
  YCHAR = CHR(179)

  LINE  = SPACE(1)+YCHAR
  XLINE = " À"+REPLICATE(XCHAR,NO_COL*LARG_COL)

  @ 00,00 SAY TITULO

  BCOUNT  = 0
  FT = Int(Round((MAXROW()-4)/5,0))

  FOR CNTR = 1 TO MAXROW()-4
    LEFT = SPACE(5)+LINE
    IF (((CNTR-1)/FT=INT((CNTR-1)/FT)).OR.CNTR=1).AND.CNTR<(MAXROW()-4)
       LEFT = STR(HR-((BCOUNT/5)*HR),5)+SPACE(1)+CHR(180)
       BCOUNT = BCOUNT + 1
    ENDIF
    ? LEFT
  NEXT

  ? STR(LR,5)+XLINE
  ? '  '+BLINE
  COLPOS = 7
  DIV    = (HR-LR)/(MAXROW()-4)
  LP  = 1
  CORF=SETCOLOR()
  FOR LP = 1 TO NO_COL
     CORB=CORES[LP]
     COL   = COLS[LP]/DIV
     ROW   = MAXROW()-4
     SETCOLOR(CORB)
     DO WHILE COL > 0
        @ ROW,COLPOS+1 SAY REPL(CARAC[LP],LARG_COL-1)
        ROW = ROW - 1
        COL = COL - 1
     ENDDO
     COLPOS = COLPOS + LARG_COL
  NEXT
  SETCOLOR(OLDCOR)
  @ 24,0 SAY ""
  RETURN(.T.)
ELSE
  IF TYPE("CARAC")="C"
     CAUX=CARAC
     CARAC:={}
     ASIZE(CARAC,LEN(COLS))
     AFILL(CARAC,CAUX)
  ELSEIF TYPE("CARAC")#"A"
     CARAC:={}
     ASIZE(CARAC,LEN(COLS))
     AFILL(CARAC,CHR(35))
  ENDIF

  NO_COL  = LEN(COLS)
  TWIDTH  = LARG_COL-1
  BLINE   = SPACE(6)
  DIV    = (HR-LR)/10

  FOR CTD = 1 TO NO_COL
   TITS[CTD] = ALLTRIM(TITS[CTD])
   TITS[CTD] = LEFT(SPACE(CENTRA(TITS[CTD],TWIDTH))+TITS[CTD],TWIDTH)
   TITS[CTD] = TITS[CTD]+IF(LEN(TITS[CTD])<TWIDTH,SPACE(TWIDTH-LEN(TITS[CTD])),"")
   BLINE     = BLINE+TITS[CTD]+SPACE(1)
  NEXT

  XCHAR = CHR(45)
  YCHAR = CHR(124)

  XLINE  = REPLICATE(XCHAR,(NO_COL*LARG_COL))
  BLANKS = SPACE(LARG_COL-1)

  ? SPACE(CENTRA(TITULO,80))+TITULO
  ?

  LCOUNT = 1
  BCOUNT = 0

  FOR LCOUNT = 1 TO 20
     GLINE     = SPACE(5)+YCHAR+" "
     IF (((LCOUNT-1)/4=INT((LCOUNT-1)/4)).OR.LCOUNT=1).AND.LCOUNT<20
        GLINE  = STR(HR-((BCOUNT/5)*HR),5)+YCHAR+" "
        BCOUNT = BCOUNT + 1
     ENDIF

     FOR CCO = 1 TO NO_COL
        GLINE = IF(LCOUNT>20-(2*(COLS[CCO]/DIV)),GLINE+REPL(CARAC[CCO],LARG_COL-1),GLINE+BLANKS)+" "
     NEXT

     ? GLINE
     CCOUNT = 1
  NEXT
  ? STR(LR,5)+"+"+XLINE
  ? " "+BLINE
  RETURN(.T.)
ENDIF
